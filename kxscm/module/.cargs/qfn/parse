// @private
//
// @author David Han
//
// @kind function
//
// @fileOverview Command line argument parser with additional functionality on top of the standard ``.Q.opt`` 
// parsing:
//
//  - Removes additional '-' when argument keys are specified in the more standard '--parameter' form
//  - All argument values are a string (10h) with multi-argument values joined with spaces between them
//
// @param cmdArgsStr {string} Command line arguments as a string
//
// @returns {dict} Argument parameter as keys with the argument values
//
// @throws IllegalArgumentException If the input is not a mixed list of strings
//
// @see .Q.opt
//
// @qlintsuppress RESERVED_NAME
{[cmdArgsStr]
  if[not .type.isMixedList cmdArgsStr;'"IllegalArgumentException";];
  
  cmdArgs:.Q.opt cmdArgsStr;
  cmdArgs:" " sv/: cmdArgs;
  
  cmdArgsK:key cmdArgs;
  cmdArgsK:@[cmdArgsK;where "-" = first each string cmdArgsK;{`$1_ string x}];
  
  :cmdArgsK!value cmdArgs;
  }
