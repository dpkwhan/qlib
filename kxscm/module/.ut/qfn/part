// @fileoverview Partition a dataset into training and test dataset.
//
// @param w {dict|number[]} A dictionary or list of numbers
// @param sf {function|integer[]|table} A sampling function
// @param x {table} Input data to split
//
// @returns {any[]} a list of datasets
//
// use (w)eight vector or dictionary to partition (x). (s)ampling (f)unction:
// til = no shuffle, 0N? = shuffle, list or table = stratify
{[w;sf;x]
 if[99h=type w;:key[w]!.z.s[value w;sf;x]];
 if[99h<type sf;:x (floor sums n*prev[0f;w%sum w]) _ sf n:count x];
 x@:raze each flip value .z.s[w;0N?] each group sf; / stratify
 x}
