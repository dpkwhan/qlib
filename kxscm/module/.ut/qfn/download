// @fileoverview Download and uncompress files.
//
// @param b {string} A base url
// @param f {string|string[]} A file of list of files
// @param e {string} Extension name of compressed files
// @param uf {function} A function to unzip compressed file
//
// @returns {symbol} The file handle to the downloaded file
//
// (b)ase url, (f)ile, (e)xtension, (u)ncompress (f)unction

{[b;f;e;uf]
 if[0h=type f;:.z.s[b;;e;uf] each f];
 if[l~key l:`$":",f;:l];                          / local file exists
 if[()~key z:`$":",f,e;z 1: .Q.hg`$":",0N!b,f,e]; / download
 if[count uf;system 0N!uf," ",f,e];               / uncompress
 l}